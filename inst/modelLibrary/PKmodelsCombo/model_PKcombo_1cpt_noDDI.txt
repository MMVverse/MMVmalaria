********** MODEL NAME
model_PKcombo_1cpt_noDDI

********** MODEL NOTES
Library model for SCID mouse combo PK modeling.

PK Drug 1:
1 compartmental distribution model, non-linear (linea+saturable), 1st order absorption

PK Drug 2:
1 compartmental distribution model, non-linear (linea+saturable), 1st order absorption

PK DDI:
None

Important assumption: INPUT dose (mg or mg/kg) and PK model matched to lead to drug 
                      concentration in ug/mL!
                     

********** MODEL STATES

# PK model Drug 1 (dosing: INPUT1)
d/dt(Adx1) = -kax1*Adx1 + Fabs1x1*INPUT1
d/dt(Acx1) =  kax1*Adx1 - CLx1*Ccx1 - (VMAXx1*Ccx1)/(KMx1+Ccx1)


# PK model Drug 2 (dosing: INPUT2)
d/dt(Adx2) = -kax2*Adx2 + Fabs1x2*INPUT2
d/dt(Acx2) =  kax2*Adx2 - CLx2*Ccx2 - (VMAXx2*Ccx2)/(KMx2+Ccx2)


********** MODEL PARAMETERS

# -------- PK parameters --------

# --- Drug 1 ---
Fabs1x1 = 1           # Relative bioavailability Drug1 (-)
kax1    = 0.1         # Absorption rate parameter Drug1 (1/hour)
CLx1    = 0           # Apparent clearance Drug1 (L/hour)
VMAXx1  = 0           # Maximum apparent clearance rate Drug1 (mg/hour)
KMx1    = 10	      # Michaelis-Menton constant Drug1 (ug/mL)
Vcx1    = 1           # Apparent central volume Drug1 (L)
Tlag1x1 = 0           # Absorption lag time Drug1 (hours)

# --- Drug 2 ---
Fabs1x2 = 1           # Relative bioavailability Drug2 (-)
kax2    = 0.1         # Absorption rate parameter Drug2 (1/hour)
CLx2    = 0           # Apparent clearance Drug2 (L/hour)
VMAXx2  = 0           # Maximum apparent clearance rate Drug2 (mg/hour)
KMx2    = 10	      # Michaelis-Menton constant Drug2 (ug/mL)
Vcx2    = 1           # Apparent central volume Drug2 (L)
Tlag1x2 = 0           # Absorption lag time Drug2 (hours)


********** MODEL VARIABLES

# -------- PK --------
# Drug 1 concentration (µg/mL)
Ccx1 = Acx1/Vcx1   

# Drug 2 concentration (µg/mL)
Ccx2 = Acx2/Vcx2


# -------- Output --------
# Central Cpt Concentration
OUTPUT1 = Ccx1        # Concentration Drug1 (ug/mL)
OUTPUT2 = Ccx2        # Concentration Drug2 (ug/mL)


********** MODEL REACTIONS


********** MODEL FUNCTIONS


********** MODEL EVENTS


